# Maintainer: Atom Long <atom.long@hotmail.com>

pkgname=sunxi-tools-git
pkgver=1.1.r487.g6c02224
pkgrel=1
pkgdesc="Tools to help hacking Allwinner based devices. (A10, A20, sun4i, sun7i)"
arch=(i686 x86_64)
url="https://github.com/linux-sunxi/sunxi-tools"
license=(GPL)
depends=(libusb)
makedepends=(git)
conflicts=(sunxi-tools)
provides=(sunxi-tools)
source=("git+https://github.com/linux-sunxi/sunxi-tools.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname%-git}"
  make
}

package() {
  cd "${pkgname%-git}"
  PREFIX="$pkgdir/usr" make install
}
