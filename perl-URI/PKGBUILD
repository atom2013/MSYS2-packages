# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=URI
pkgname=perl-${_realname}
pkgver=5.07
pkgrel=1
pkgdesc="Uniform Resource Identifiers (absolute and relative)"
arch=('any')
url="https://metacpan.org/${_realname}/"
groups=('perl-modules')
license=('PerlArtistic')
depends=('perl>=5.10.0')
checkdepends=('perl-Test-Needs')
provides=('perl-URI-Escape=3.30')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/${_realname}-${pkgver}.tar.gz")
sha256sums=('eeb6ed2ae212434e2021e29f7556f4024169421a5d8b001a89e65982944131ea')

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"
}
