# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=dulwich
pkgname="python-${_realname}"
pkgver=0.19.16
pkgrel=1
pkgdesc='Pure-Python implementation of the Git file formats and protocols'
arch=('i686' 'x86_64')
url='https://www.dulwich.io'
license=('GPL')
depends=('python')
makedepends=('python-setuptools' 'libcrypt-devel')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('f74561c448bfb6f04c07de731c1181ae4280017f759b0bb04fa5770aa84ca850')

prepare(){
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  /usr/bin/python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  # We are missing runtime deps, so don't bother installing the cli tools.
  # dulwich alone is enough to get breezy working.
  rm -Rf "${pkgdir}"/usr/bin

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
