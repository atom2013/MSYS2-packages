# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=setuptools
pkgbase="python2-${_realname}"
pkgname=("python2-${_realname}")
pkgver=44.1.0
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
depends=('python2')
provides=('python2-distribute')
replaces=('python2-distribute')
url="https://pypi.python.org/pypi/setuptools"
source=("${_realname}-${pkgver}.tar.gz::https://github.com/pypa/setuptools/archive/v${pkgver}.tar.gz")
sha512sums=('b3b01e7f2aa063249a9147a78ac1c92527628b56650f5f53f008b5d86b7ac88c9f59ece5527489095211dd4791103e87d7fd8bedc10c422d562a51757a678fe4')

prepare() {
  # Don't download and install deps
  sed -i '/pip.main(args)/d' setuptools-${pkgver}/bootstrap.py

  # Remove post-release tag since we are using stable tags
  sed -e '/tag_build = .post/d' \
      -e '/tag_date = 1/d' \
      -i setuptools-${pkgver}/setup.cfg

  cd "${srcdir}"/setuptools-${pkgver}
  sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python2|" setuptools/command/easy_install.py
  sed -i -e "s|'pip'|'pip2'|" setuptools/tests/{test_develop.py,test_namespaces.py}

  export SETUPTOOLS_INSTALL_WINDOWS_SPECIFIC_FILES=0
}

build() {
  # Build python 2 module
  cd "${srcdir}"/setuptools-${pkgver}
  /usr/bin/python2 bootstrap.py
  /usr/bin/python2 setup.py build
}

check() { (
  # Workaround UTF-8 tests by setting LC_CTYPE
  export LC_CTYPE=en_US.UTF-8

  # https://github.com/pypa/setuptools/pull/810
  export PYTHONDONTWRITEBYTECODE=1

  # Check python2 module
  # cd "${srcdir}"/setuptools-${pkgver}
  # /usr/bin/python2 setup.py pytest
)}

package() {
  cd "${srcdir}"/setuptools-${pkgver}

  /usr/bin/python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  rm "${pkgdir}"/usr/bin/easy_install
}
