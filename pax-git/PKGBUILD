# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_pkgname=pax
pkgname=${_pkgname}-git
pkgver=20171021
pkgrel=1
pkgdesc="POSIX standard archive tool"
arch=('i686' 'x86_64')
groups=('base')
depends=('msys2-runtime')
makedepends=('git')
url="https://www.openbsd.org/cgi-bin/cvsweb/src/bin/pax/"
license=('BSD')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}"::"git+https://github.com/MirBSD/mircpio"
        Makefile.msys)
sha256sums=('SKIP'
            'c4864ee6738dde01073bb690fea997153ce22116cc80930032352d14749a9303')

pkgver() {
  cd ${srcdir}/${_pkgname}
  git tag | sed -e 's|paxmirabilis.||g' | grep -Po '^\d+$' | tail -n1
}

prepare() {
  rm -f ${srcdir}/${_pkgname}/Makefile.msys
  cp ${srcdir}/Makefile.msys ${srcdir}/${_pkgname}
}

build() {
  cd ${srcdir}/${_pkgname}
  make -f Makefile.msys
}

package() {
  cd ${srcdir}/${_pkgname}
  make -f Makefile.msys DESTDIR=${pkgdir} install
}
