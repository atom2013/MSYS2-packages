# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=("aspell" "aspell-devel")
_pkgver=0.60.7-rc1
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc="A Free and Open Source spell checker (libraries)"
arch=('i686' 'x86_64')
url="http://aspell.net/"
license=('LGPL')
groups=('libraries')
makedepends=('gcc' 'gettext-devel' 'libiconv-devel' 'ncurses-devel')
options=()
source=(#"https://ftp.gnu.org/gnu/aspell/${pkgname}-${pkgver}.tar.gz"{,.sig}
        "https://github.com/GNUAspell/aspell/archive/rel-${_pkgver}.tar.gz"
        "aspell-noundefined.patch"
        "aspell-so2dll.patch"
        "aspell-fileno.patch")
sha256sums=('eeeddc742e76dcd1e7762b64fb68872cf83ea9681b3432a8ffdbc820ade86c0f'
            '5188d1443b04c095261b3fe01f77a9235ca4b3c5c35caa87167a5507a30f6bee'
            '9b9e4300d35a92f029c098da7001235d1dd6aeb03a854591afd84606b5cd257b'
            '8054609ba63038c7f488782a1f80ed31ba1fe18ded842707702ad08793903c7b')

prepare() {
  cd "${srcdir}/${pkgname}-rel-${_pkgver}"
  patch -p1 -i ${srcdir}/aspell-noundefined.patch
  patch -p1 -i ${srcdir}/aspell-so2dll.patch
  patch -p1 -i ${srcdir}/aspell-fileno.patch
  PERL_USE_UNSAFE_INC=1 ./autogen
  #autoreconf -ivf
}

build() {
  cd "${srcdir}/${pkgname}-rel-${_pkgver}"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --build=${CHOST} \
    --enable-static

  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${srcdir}/${pkgname}-rel-${_pkgver}"
  make -k check
}

package_aspell() {
  depends=("gcc-libs" "gettext" "libiconv" "ncurses")

  mkdir -p ${pkgdir}/usr/bin
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/

  mkdir -p ${pkgdir}/usr/lib/aspell-0.60
  cp -rf ${srcdir}/dest/usr/lib/aspell-0.60 ${pkgdir}/usr/lib/

  mkdir -p ${pkgdir}/usr/share
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_aspell-devel() {
  pkgdesc="Aspell headers and libraries (development)"
  groups=('development')
  depends=("aspell=${pkgver}" "gettext-devel" "libiconv-devel" "ncurses-devel")

  mkdir -p ${pkgdir}/usr/lib
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -f ${srcdir}/dest/usr/lib/*.a ${pkgdir}/usr/lib
}
