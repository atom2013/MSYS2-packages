# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgbase=gmp
pkgname=('gmp' 'gmp-devel')
pkgver=6.2.0
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic"
groups=('libraries')
arch=('i686' 'x86_64')
url="https://gmplib.org/"
license=('LGPL3')
install=gmp.install
source=(https://gmplib.org/download/gmp-${pkgver}/gmp-${pkgver}.tar.xz{,.sig}
        gmp-6.2.0-1.src.patch
        gmp-6.2.0-msys2.patch)
sha256sums=('258e6cd51b3fbdfc185c716d55f82c08aff57df0c6fbd143cf6ed561267a1526'
            'SKIP'
            'f8a7f986f8d646ef6ce2e3b8312ad3a6cc9e219e59d712b3ff30de995c857be2'
            'fd1330261fb85c0b2b820535b58084a2ce6cfafe9a953cb9cfaa4dd46b4ab6c3')
validpgpkeys=('5972C48224C1E7E62AA0275D1150EE33C1A000B0'   # Marco Bodrato
              '343C2FF0FBEE5EC2EDBEF399F3599FF828C67298')   # Niels MÃ¶ller

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

apply_patch_p2_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp2 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  apply_patch_p2_with_msg gmp-6.2.0-1.src.patch
  apply_patch_with_msg gmp-6.2.0-msys2.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
# get rid of those pesky declspec warnings
  CFLAGS+=" -Wno-attributes" \
  CXXFLAGS+=" -Wno-attributes" \
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --enable-cxx \
    --enable-fat \
    --enable-shared --disable-static
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package_gmp() {
  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/bin ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/share ${pkgdir}/usr/
}

package_gmp-devel() {
  groups=('development')
  options=(staticlibs)
  depends=("gmp=${pkgver}")
  pkgdesc="GMP headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/include ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/lib ${pkgdir}/usr/
}
