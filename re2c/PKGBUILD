# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
# stolen from Archlinux

pkgname=re2c
pkgver=1.2.1
pkgrel=1
arch=('i686' 'x86_64')
depends=(gcc-libs)
pkgdesc='A tool for generating C-based recognizers from regular expressions'
url='https://re2c.org/'
license=('public domain')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/skvadrik/re2c/archive/${pkgver}.tar.gz)
sha256sums=('2d361e8f8c10384cc9e0bb29db853d136479cc132362773035d199b0367d2f86')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make tests
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
