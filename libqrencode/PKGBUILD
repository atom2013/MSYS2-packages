# Maintainer: micbou <contact@micbou.com>

pkgbase=libqrencode
pkgname=('libqrencode' 'libqrencode-devel')
pkgver=4.0.2
pkgrel=1
pkgdesc="QR Code encoding library"
conflicts=(${pkgbase}-git)
replaces=(${pkgbase}-git)
arch=('i686' 'x86_64')
url='https://fukuchi.org/works/qrencode/'
license=('LGPL')
#depends=(libpng")
makedepends=('autoconf' 'automake' 'libtool' 'pkg-config' 'gcc')
source=("https://github.com/fukuchi/libqrencode/archive/v${pkgver}.tar.gz")
sha256sums=('43091fea4752101f0fe61a957310ead10a7cb4b81e170ce61e5baa73a6291ac2')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./autogen.sh
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./configure \
    --prefix=/usr \
    --build=${CHOST}

  make LDFLAGS="${LDFLAGS} -no-undefined"
  make DESTDIR=${srcdir}/dest install
}

package_libqrencode() {
  groups=('libraries')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/

  mkdir -p ${pkgdir}/usr/share
  cp -rf ${srcdir}/dest/usr/share/man ${pkgdir}/usr/share/
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_libqrencode-devel() {
  pkgdesc="libqrencode headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=(libqrencode=${pkgver})

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}