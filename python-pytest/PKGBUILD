# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=python-pytest
pkgname=('python3-pytest' 'python2-pytest')
pkgver=4.6.0
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
makedepends=('python2-atomicwrites'
             'python3-atomicwrites'
             'python2-attrs'
             'python3-attrs'
             'python2-funcsigs'
             'python2-pathlib2'
             'python2-pluggy'
             'python3-pluggy' 
             'python2-py'
             'python3-py'
             'python2-setuptools'
             'python3-setuptools' 
             'python2-setuptools-scm'
             'python3-setuptools-scm'
             'python2-six'
             'python3-six')
source=("pytest-${pkgver}.tar.gz::https://github.com/pytest-dev/pytest/archive/${pkgver}.tar.gz")
sha512sums=('7439a6f27c349f668c9f6b507109782eb56199c565b7ca386f9a420edcb3183ae9b447be0b507247589c3cc74b6a376de39ea05f8b65be2fbb2c04315e2cafa0')

prepare() {
  sed -i "s/'_pytest.vendored_packages'//" pytest-${pkgver}/setup.py
  sed -i 's/assert numentries == 0/assert numentries == 26/' pytest-${pkgver}/testing/python/collect.py
  cp -a pytest-${pkgver}{,-py2}
}

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  
  cd "${srcdir}/pytest-${pkgver}"
  python3 setup.py build

  cd "${srcdir}/pytest-${pkgver}-py2"
  python2 setup.py build
}

package_python3-pytest() {
  depends=('python3' 
           'python3-atomicwrites'
           'python3-attrs'
           'python3-more-itertools'
           'python3-pluggy'
           'python3-py'
           'python3-setuptools'
           'python3-six')

  cd pytest-${pkgver}
  python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pytest() {
  depends=('python2' 
           'python2-atomicwrites'
           'python2-attrs'
           'python2-more-itertools'
           'python3-pluggy'
           'python2-py'
           'python2-setuptools'
           'python3-six')

  cd pytest-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  for f in {py.test,pytest}; do
    mv "${pkgdir}"/usr/bin/${f}{,2}
  done
}

