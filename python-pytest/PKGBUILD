# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=python-pytest
pkgname=('python-pytest')
pkgver=6.1.1
pkgrel=1
provides=("python3-pytest")
replaces=("python3-pytest")
conflicts=("python3-pytest")
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python'
         'python-atomicwrites'
         'python-attrs'
         'python-more-itertools'
         'python-pluggy'
         'python-py'
         'python-setuptools'
         'python-six')
makedepends=('python-setuptools-scm')
source=("pytest-${pkgver}.tar.gz::https://github.com/pytest-dev/pytest/archive/${pkgver}.tar.gz")
sha512sums=('642782eb3f07756c11c7552c908031e83d888d7dbdd11f5b7aa349b6406724349468c6beee5f1ef770a4522710301659db7f9d6100f8da4b43c72d79d077f2aa')

prepare() {
  sed -i "s/'_pytest.vendored_packages'//" pytest-${pkgver}/setup.py
  sed -i 's/assert numentries == 0/assert numentries == 26/' pytest-${pkgver}/testing/python/collect.py
}

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  
  cd "${srcdir}/pytest-${pkgver}"
  /usr/bin/python setup.py build
}

package() {
  cd pytest-${pkgver}
  /usr/bin/python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
