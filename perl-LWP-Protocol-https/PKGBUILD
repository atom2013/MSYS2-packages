# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=LWP-Protocol-https
pkgname=perl-${_realname}
pkgver=6.09
pkgrel=1
pkgdesc="Provide https support for LWP::UserAgent"
arch=('any')
url="https://metacpan.org/release/LWP-Protocol-https"
groups=('perl-modules')
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-IO-Socket-SSL' 'perl-Mozilla-CA' 'perl-Net-HTTP' 'perl-libwww')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/O/OA/OALDERS/${_realname}-$pkgver.tar.gz")
sha256sums=('16cfe1a511690b066db5667c8714802ee2b9c5d28a31a3e7bd36fbc70a3af592')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="$pkgdir" install
}
