# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-pathlib2
pkgver=2.3.4
pkgrel=1
pkgdesc='Object-oriented filesystem paths'
arch=('any')
url='https://pypi.python.org/pypi/pathlib2'
license=('MIT')
depends=('python2-six' 'python2-scandir')
makedepends=('python2-setuptools')
checkdepends=('python2-pytest-runner' 'python2-mock')
source=("https://pypi.io/packages/source/p/pathlib2/pathlib2-$pkgver.tar.gz")
sha256sums=('446014523bb9be5c28128c4d2a10ad6bb60769e78bd85658fe44a450674e0ef8')

build() {
  cd pathlib2-$pkgver
  python2 setup.py build
}

check() {
  cd pathlib2-$pkgver
  python2 setup.py pytest || warning "Tests failed"
}

package() {
  cd pathlib2-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/python2-pathlib2/LICENSE.rst
}
