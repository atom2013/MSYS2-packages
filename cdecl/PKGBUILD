# Maintainer: John Butera <john@jbutera.net>

_realname=cdecl-blocks
pkgname=cdecl
pkgver=2.5
pkgrel=1
pkgdesc="A program for encoding and decoding C (or C++) type declarations"
arch=('i686' 'x86_64')
url="https://cdecl.org/"
options=('strip' 'staticlibs' '!makeflags')
license=('public-domain')
depends=("libedit")
makedepends=("gcc" 'make' 'libedit-devel' 'flex' 'bison')
source=("https://cdecl.org/files/cdecl-blocks-2.5.tar.gz"
        "build.patch")
sha256sums=('9ee6402be7e4f5bb5e6ee60c6b9ea3862935bf070e6cecd0ab0842305406f3ac'
            'd49bb2fd4d0b88e9ee8dc6d9f9e5d15414185885f6c5d78f43807b5757cf0d3a')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/build.patch"
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  make CFLAGS="-O2 -s -DUSE_READLINE -std=gnu90"
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make PREFIX="${pkgdir}" install
}
