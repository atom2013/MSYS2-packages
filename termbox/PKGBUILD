# Maintainer: Yusuke Sasaki <yusuke dot sasaki dot nuem at gmail dot com>

pkgname=termbox
pkgver=1.1.2
pkgrel=1
pkgdesc="Library for writing text-based user interfaces"
_wafver=2.0.21
arch=('i686' 'x86_64')
license=('MIT')
url="https://github.com/nsf/termbox"
makedepends=('python3')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/nsf/termbox/archive/v${pkgver}.tar.gz
        https://waf.io/waf-${_wafver}
        0010-msysize.patch)
sha256sums=('61c9940b42b3ac44bf0cba67eacba75e3c02088b8c695149528c77def04d69b1'
            '7cebf2c5efe53cbb9a4b5bdc4b49ae90ecd64a8fce7a3222d58e591b58215306'
            'ad0550bd4e66daee3f6c3cfcc01c3f12e8b2e415f87deae910df8cfbe0f4ee35')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  cp -f ${srcdir}/waf-${_wafver} ./waf

  patch -p1 -i ${srcdir}/0010-msysize.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  /usr/bin/python waf configure --prefix=/usr
  /usr/bin/python waf build -v
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  /usr/bin/python waf install --destdir="${pkgdir}"
}
