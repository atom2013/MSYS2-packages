# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTML-Parser
pkgname=perl-${_realname}
pkgver=3.75
pkgrel=1
pkgdesc="Perl HTML parser class"
arch=('i686' 'x86_64')
license=('PerlArtistic')
url="https://metacpan.org/release/HTML-Parser"
groups=('perl-modules')
depends=('perl-HTML-Tagset' 'perl-HTTP-Message' 'perl-URI' 'perl')
makedepends=('perl-devel')
checkdepends=('perl-Test-Pod')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/C/CA/CAPOEIRAB/${_realname}-${pkgver}.tar.gz)
sha256sums=('ac6b5e25a8df7af54885201e91c45fb9ab6744c08cedc1a38fcc7d95d21193a9')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make install DESTDIR="${pkgdir}"
}
