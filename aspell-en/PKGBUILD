# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=aspell-en
pkgname=aspell6-en
pkgver=2019.10.06
_pkg_subver=0
pkgrel=1
pkgdesc="Aspell English dictionaries"
arch=('i686' 'x86_64')
url="https://ftp.gnu.org/gnu/aspell/dict/0index.html"
license=('custom')
groups=()
makedepends=('aspell-devel')
depends=('aspell>=0.60')
provides=('aspell-en')
options=()
source=("https://ftp.gnu.org/gnu/aspell/dict/en/${pkgname}-${pkgver}-${_pkg_subver}.tar.bz2"{,.sig})
sha256sums=('24334b4daac6890a679084f4089e1ce7edbe33c442ace776fa693d8e334f51fd'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-${_pkg_subver}"
  ./configure
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-${_pkg_subver}"
  make DESTDIR="${pkgdir}/" install

  install -Dm644 Copyright ${pkgdir}/usr/share/licenses/aspell-en/Copyright
}
