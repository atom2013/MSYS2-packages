# $Id$
# Maintainer: David Mott <mott.david.j@gmail.com>

pkgname=dialog
pkgver=1.3_20210324
pkgrel=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('i686' 'x86_64')
url="https://invisible-island.net/dialog/"
license=('LGPL2.1')
makedepends=('ncurses-devel')
depends=('ncurses')
source=(https://invisible-mirror.net/archives/$pkgname/$pkgname-${pkgver/_/-}.tgz)
sha256sums=('01c2d1e2e9af9b083ea200caad084fdfda55178d5bbf4e42c9fff44935151653')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  ./configure \
    --prefix=/usr \
    --enable-nls \
    --with-ncursesw
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install-full
}
