# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname="crosstool-ng"
pkgname="$_realname-git"
replaces=("$_realname")
pkgver=r4738.eb72b4eb
pkgrel=1
pkgdesc="A cross-platform open-source toolchain system (git)"
arch=('i686' 'x86_64')
url="http://www.crosstool-ng.org/"
license=("MIT")
makedepends=("autoconf" "automake-wrapper" "binutils" "bison"
             "flex" "gettext-devel" "git" "gcc" "gperf"
             "patch" "libtool" "make" "ncurses-devel"
             "tar" "texinfo" "wget" "xz" "unzip")
depends=("ncurses" "libintl")
options=('staticlibs' 'strip')
source=('crosstool-ng::git+https://github.com/crosstool-ng/crosstool-ng.git#branch=master'
		"0001-Modify-config-to-support-correct-Mingw64-Triplet-Nam.patch"
		"0002-modify-kconfig-Makefile-to-include-lintl.patch")
sha256sums=('SKIP'
			'08c162f8199c45def9ac92098110b6b58c683c1561a04a7d475334a83b31ca1c'
			'c39c0971d09f217efc440a99a68bdec0b3c34d8c3b00c310f46f03348c225423')

pkgver() {
  cd "${srcdir}/${_realname}"
  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  
  ./bootstrap
  
  patch -p1 -i "${srcdir}/0001-Modify-config-to-support-correct-Mingw64-Triplet-Nam.patch"
  patch -p1 -i "${srcdir}/0002-modify-kconfig-Makefile-to-include-lintl.patch"
  
  find packages/ncurses -type f -name "*.patch" | xargs dos2unix
}

build() {
  cd "${srcdir}/${_realname}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_realname}"
  make DESTDIR=${pkgdir} install
}
