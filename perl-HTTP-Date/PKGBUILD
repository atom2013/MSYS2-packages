# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTTP-Date
pkgname=perl-${_realname}
pkgver=6.05
pkgrel=1
pkgdesc="Date conversion routines"
arch=('any')
license=('PerlArtistic' 'GPL')
groups=('perl-modules')
options=('!emptydirs')
depends=('perl>=5.8.8')
conflicts=('perl-libwww<6')
url="https://metacpan.org/release/HTTP-Date"
source=(https://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/${_realname}-$pkgver.tar.gz)
sha256sums=('365d6294dfbd37ebc51def8b65b81eb79b3934ecbc95a2ec2d4d827efe6a922b')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${_realname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${_realname}-${pkgver}"
    make test
  )
}

package() {
  cd "${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
