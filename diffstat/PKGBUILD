# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=diffstat
pkgname=diffstat
pkgver=1.63
pkgrel=1
pkgdesc="Display a histogram of diff changes"
arch=('i686' 'x86_64')
url="https://invisible-island.net/diffstat"
groups=('base-devel')
license=('MIT')
source=("https://invisible-mirror.net/archives/${pkgbase}/${pkgbase}-${pkgver}.tgz"{,.asc}
        diffstat-1.62-msys2.patch)
sha256sums=('7eddd53401b99b90bac3f7ebf23dd583d7d99c6106e67a4f1161b7a20110dc6f'
            'SKIP'
            'b0dba10dfb4dd41fb0eacb1daf315bfa466889c9289682c04931d3887051d7fa')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/diffstat-1.62-msys2.patch
}

build () {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --build=${CHOST} \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir}/ install
  install -Dm644 package/debian/copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
