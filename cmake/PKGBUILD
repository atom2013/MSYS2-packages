# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>

_realname=cmake
pkgname=("${_realname}" "${_realname}-emacs" "${_realname}-vim")
pkgver=3.18.4
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('i686' 'x86_64')
url="https://www.cmake.org/"
license=(MIT)
makedepends=('gcc'
             'emacs'
             'jsoncpp-devel'
             'libarchive-devel'
             'libbz2-devel'
             'libcurl-devel'
             'libexpat-devel'
             'liblzma-devel'
             'librhash-devel'
             'libutil-linux-devel'
             'libuv-devel'
             'libzstd-devel'
             'ncurses-devel'
             'zlib-devel')
depends=('gcc-libs'
         'jsoncpp'
         'libcurl'
         'libexpat'
         'libarchive'
         'librhash'
         'libutil-linux'
         'libuv'
         'ncurses'
         'pkg-config'
         'zlib')
options=('staticlibs' 'strip')
source=(https://www.cmake.org/files/v${pkgver%.*}/${pkgname}-${pkgver}.tar.gz
        perl-libs.patch
        3.17.3-case-sensitivity.patch
        3.17.3-opengl.patch
        3.17.3-findtclsh.patch
        3.17.3-cpuinfo.patch
        3.17.3-cygwin-paths.patch
        cmake-3.18.4-msys.patch
        disable-response-files-for-msys.patch)
sha256sums=('597c61358e6a92ecbfad42a9b5321ddd801fc7e7eca08441307c9138382d4f77'
            'dae002351594910cf47d2f11956ddffc6cd9c8f755204944886b43dba0f5d291'
            'babee378f4b767dbd16d220ad92ef5556759dc34910e01322e3f3b1dc1b605ed'
            '081d625176342ad6f62ad2bcf141db2d4121d654749a0acf511f86fe8112d94d'
            'f98ca4af7cd986d6222918d07117ae828ee7398f25ec5ca23a9656b216e6d0fe'
            '95687ae482f9a312080291a341a79c122c7a293de3d4f4dff44f5b36faac099d'
            'fc11babbc9444ec9cb52e8ac05def51d4be06605b7edefb9ea6e74a5a75280a5'
            'c89592e6e08c20e814f36d89c3e93349384f197100e5bf88286dca01d6aa099f'
            'ffde15cbf2cbe3f5f3f0301722d9941bfa94396f667fdc911323b8b3f69dfbc6')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _fname in $@
  do
    msg2 Applying ${_fname}
    patch -Nbp1 -i ${srcdir}/${_fname}
  done
}

del_file_exists() {
  for _fname in $@
  do
    if [ -f $_fname ] || [ -d $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd ${pkgname}-${pkgver}
  del_file_exists Modules/CPackMsys.cmake \
    Modules/FindMsys.cmake \
    Modules/Platform/MSYS-Clang-C.cmake \
    Modules/Platform/MSYS-Clang-CXX.cmake \
    Modules/Platform/MSYS-Determine-CXX.cmake \
    Modules/Platform/MSYS-GNU.cmake \
    Modules/Platform/MSYS.cmake \
    Modules/Platform/MSYS-CXX.cmake \
    Modules/Platform/MSYS-GNU-C.cmake \
    Modules/Platform/MSYS-GNU-CXX.cmake \
    Modules/Platform/MSYS-GNU-Fortran.cmake \
    Modules/Platform/MSYS-windres.cmake || true
    
  # Cygwin patches
  apply_patch_with_msg \
       perl-libs.patch \
       3.17.3-case-sensitivity.patch \
       3.17.3-opengl.patch \
       3.17.3-findtclsh.patch \
       3.17.3-cpuinfo.patch \
       3.17.3-cygwin-paths.patch

  # Msysize
  apply_patch_with_msg \
       cmake-3.18.4-msys.patch \
       disable-response-files-for-msys.patch
}

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH}
  cd ${srcdir}/build-${CARCH}
  MSYSTEM=MSYS ${srcdir}/${_realname}-${pkgver}/configure \
    --prefix=/usr \
    --system-libs \
    --no-qt-gui \
    --parallel=$(nproc) \
    --mandir=share/man \
    --docdir=share/doc/${_realname} \
    -- -DCURSES_FORM_LIBRARY=/usr/lib/libformw.dll.a \
      -DCTEST_TEST_CTEST=ON \
      -DCMAKE_BUILD_TYPE=Release
  #Fix tests
  local  _gccver=$(gcc --version | grep ^gcc | sed 's/^.* //g')
  sed -e "s|4.9.2|${_gccver}|g" -e "s|4.8.2|${_gccver}|g" \
       -i ${srcdir}/build-${CARCH}/Tests/CMakeTests/ImplicitLinkInfoTest.cmake
  plain "Start building..."
  make
  make DESTDIR=${srcdir}/dest install
}

#Enable once we can get all tests to pass
#check() {
#  cd ${srcdir}/build-${CARCH}
#  make test
#}


package_cmake() {
  cd ${srcdir}/dest
  
  install -d  ${pkgdir}/usr/bin/
  install -d  ${pkgdir}/usr/share/aclocal
  install -d  ${pkgdir}/usr/share/${_realname}-${pkgver}
  
  cp -r usr/bin/* ${pkgdir}/usr/bin/
  cp -r usr/share/aclocal/* ${pkgdir}/usr/share/aclocal/
  cp -r usr/share/cmake-${pkgver}/* ${pkgdir}/usr/share/cmake-${pkgver}/

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/Copyright.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_cmake-vim() {
  pkgdesc="A cross-platform open-source make system (vim mode)"
  depends=("cmake=${pkgver}" 'vim')
  cd ${srcdir}/dest
  install -d ${pkgdir}/usr/share

  cp -rf usr/share/vim ${pkgdir}/usr/share/
}

package_cmake-emacs() {
  pkgdesc="A cross-platform open-source make system (Emacs mode)"
  depends=(cmake=${pkgver} 'emacs')
  cd ${srcdir}/dest

  install -d ${pkgdir}/usr/share
  cp -rf usr/share/emacs ${pkgdir}/usr/share/

  /usr/bin/emacs -batch -f batch-byte-compile \
    ${pkgdir}/usr/share/emacs/site-lisp/cmake-mode.el
}

